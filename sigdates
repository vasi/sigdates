#!/usr/bin/perl
# (C) 2006-2012 Dave Vasilevsky <dave@vasilevsky.ca>
# Licensing: Simplified BSD License, see LICENSE file
use warnings;
use strict;

use Date::Manip;
use Getopt::Long;

## CMDLINE
my $usage = <<'USAGE';
Usage: sigdates [--min=DATE] [--max=DATE] [--sigdigs=NUM] [START]
       sigdates [-n    DATE] [-x    DATE] [-s        NUM] [START]
       sigdates --help

Find 'significant dates' following the START date.

  $ sigdates --min=2002 'August 12, 1981'
     2003 Jul  8, 00:00:00             8,000 days
     2003 Oct 17, 20:26:40       700,000,000 seconds

The default start is today. Dates will only be found between the given minimum 
and maximum dates. All dates can be passed in almost any reasonable format.
If a date starts with + or -, it will be taken as a delta.

Options:

  --min=DATE, -n DATE    Beginning of time period to look for dates.
                         Default: today

  --max=DATE, -x DATE    End of time period. If a delta, relative to min.
                         Default: min +2 years

  --sigdigs=NUM, -s NUM  Number of significant digits to look for. Eg, -s0
                         will find dates like '10 days', '100 weeks', while
                         -s2 will also find '450 weeks' and '23 days'.
                         Default: 1

  --base=NUM, -b NUM     Base to use for determining significant numbers.
                         Default: 10

  --scientific, -e       Display numbers using scientific notation.
                         Default: off if base is 10, on otherwise

USAGE

my $start = "today";
my $min;
my $max;
my $base = 10;
my $sci;
my $sigdigs = 1;
my $help = 0;
my $debug = 0;

Getopt::Long::Configure(qw(bundling));
my $nerr = GetOptions(
	'min|n=s'		=> \$min,
	'max|x=s'		=> \$max,
	'sigdigs|s=i'	=> \$sigdigs,
	'base|b=i'		=> \$base,
	'scientific|e!'	=> \$sci,
	'help|h'		=> \$help,
	'debug|d'		=> \$debug,
);
$start = shift if @ARGV;
$min = 'today' unless defined $min;
$max = '+2 years' unless defined $max;
$sci = ($base == 10 ? 0 : 1) unless defined $sci;

if (!$nerr or $help or @ARGV) {
	print $usage;
	exit 1;
}


## Setup
sub my_date {
	my $spec = shift;
	my $rel = shift || 'today';
	if ($spec =~ /^[-+]/) {
		return DateCalc($rel, $spec);
	} else {
		return ParseDateString($spec);
	}
}

my ($mind, $startd) = map { my_date($_) } ($min, $start);
my $maxd = my_date($max, $mind);
my @multiples = powers(sigdigs => $sigdigs, base => $base);
my @units = qw(years months weeks days hours minutes seconds);

## Generate powers of ten, with appropriate prefices
sub powers {
	my %opts = (sigdigs => 0, @_);
	my $base = $opts{base};
	my $maxe = $opts{maxexp} || (log(1e11) / log($base));
	
	my @exponents = (0..$maxe);
	my @mults = (1..($base ** $opts{sigdigs}));
	
	my @vals = map { $base ** $_ } @exponents;
	@vals = map { my $v = $_; map { $_ * $v } @mults } @vals;
	my %vals = map { $_ => 1 } @vals;
	@vals = sort { $a <=> $b } keys %vals; # remove dups
	return (0, @vals);
}


## Some useful stuff for limiting bad multiples
sub delta_sec {	# Seconds in a delta
	Delta_Format(shift, 0, '%st'); 
}
sub unit_sec {	# Seconds in a unit
	delta_sec(DateCalc($startd, DateCalc($startd, "+1 ".shift)));
}
my $min_sec = delta_sec(DateCalc($startd, $mind));	# Seconds to min,
my $max_sec = delta_sec(DateCalc($startd, $maxd));	# max.

sub unit_filt {	# For the given unit, return only reasonable values of the
	my ($unit, @mults) = @_;	# given multiples.
	my $margin = 1.3;
	my $usec = unit_sec($unit);
	my $maxu = $max_sec / $usec * $margin;
	my $minu = $min_sec / $usec / $margin;
	grep { $_ >= $minu && $_ <= $maxu } @mults;
}


## Find all the applicable dates
my @dates;
for my $unit (@units) {
	for my $mult (unit_filt($unit, @multiples)) {
		print "Calculating $mult $unit\n" if $debug;
		eval {
			my $date = DateCalc($startd, "+ $mult $unit");
			if (Date_Cmp($date, $mind) >= 0 && Date_Cmp($date, $maxd) <= 0) {
				push @dates, { date => $date, mult => $mult, unit => $unit };
			}
		};
	}
}

## Print 'em
sub format_num { # Add some nice commas?
	my ($n, $base, $sci) = @_;
	if ($sci) {
		my $pow = log($n) / log($base);
		my $p = int($pow);
		my $m = int(sprintf "%d", $base ** ($pow - $p));
		my $fmt = "%d^%2d";
		if ($m == 1) {
			return sprintf $fmt, $base, $p;
		} else {
			return sprintf "%d x $fmt", $m, $base, $p;
		}
	} else {
		while ($n =~ s/(\d)(\d{3})(\D|$)/$1,$2$3/) { }
		return $n;
	}
}

@dates = sort { Date_Cmp($a->{date}, $b->{date}) } @dates;
my $last_date;
for my $date (@dates) {
	my $datestr = UnixDate($date->{date}, "%Y %h %e, %T");
	if (defined $last_date && Date_Cmp($last_date, $date->{date}) == 0) {
		$datestr = ""; # duplicate
	}
	
	printf "%25s %17s %s\n", $datestr, format_num($date->{mult}, $base, $sci),
		$date->{unit};
	$last_date = $date->{date};
}
